/* 작성자: 수식지왕 */
input: af(0.02), maxAF(0.2);
var: 추세(0), 당일(0), 극대(0), 극저(0), 가속(0), 내일(0);
//SAR=(EP-전일SAR)*AF+전일SAR

if i==0 then {                             //변수 초기화
   극대 = h;
   극저 = l;
   추세 = 0;
   당일 = 0;
   내일 = 0;
}
 
if 당일[1] == 0  then {                    //아직 당일 SAR이 산출되지 않은 상태
   if 추세[1] == 0 then {                  //추세가 정해지지 않은 초기 상태
      if (c > c[1]) then 추세 =  1;        //상승추세로 설정
      if (c < c[1]) then 추세 = -1;        //하락추세로 설정
   }
   if 추세[1] == 1 then {                         
      if (c < c[1]) then {                 //상승에서 하락으로 전환되면
         추세 = -1;                        //추세는 하락으로 설정
         당일 = 극대[1];                   //당일SAR은 이전 bar의 EP(+)로 계산(⑦)
         가속 = af;                        //가속은 af 초기치로(⑥)
      }
   } 
   if 추세[1] == -1 then {                        
      if (c > c[1]) then {                 //하락에서 상승으로 전환되면 
         추세 = 1;                         //추세는 상승
         당일 = 극저[1];                   //당일 SAR은 이전 bar의 EP(-)로 계산(⑦)
         가속 = af;                        //가속은 af 초기치로(⑥)
      }
   }
   극저 = min(l,극저);                     //EP(-)는 현재까지의 최저가(①)
   극대 = max(h,극대);                     //EP(+)는 현재까지의 최고가
   }
if 당일[1] > 0 then {                      //당일SAR이 한번이라도 산출되고 난 후
   if 추세[1] == -1 then {                 //하락추세에서
      if h < 내일[1] then {                //고가가 내일SAR 보다 낮으면(하락이 계속 유효)
         당일 = 내일[1];                   //당일SAR은 이전 bar에서 산출한 SAR 값을 그대로 사용
         극대 = 0;                               
         if l < 극저[1] then {             //신저가가 발생하면
            극저 = l;                      //EP(-)는 신저가로 대체(③)
            가속 = min(maxAF,가속+af);     //AF는 0.02씩 증가하되 0.2를 못 넘음(⑤)
            }
         }   
      else {                               //고가가 내일SAR을 상향돌파했다면
           추세 = 1;                       //추세는 상승으로 전환되고
           당일 = 극저[1];                 //당일SAR은 EP(-)값으로 대체(⑧)
           극저 = 0;                       //상승추세로 전환되었으므로 EP(-)는 0이 되고(④)
           극대 = h;                       //EP(+)는 현재 고가로(⑨)
           가속 = af;                      //AF는 최초 값으로(⑥)
           }
      }
   if 추세[1] == 1 then {                  //상승추세에서
      if l > 내일[1] then {                //저가가 내일SAR보다 높으면(상승이 계속 유효)
         당일 = 내일[1];                   //당일SAR은 이전 bar에서 산출한 SAR 값을 그대로 사용
         극저 = 0;
         if h > 극대[1] then {             //신고가가 발생하면
            극대 = h;                      //EP(+)는 신고가로 대체(②)
            가속 = min(maxAF,가속+af);     //AF는 0.02씩 증가하되 0.2를 못 넘음(⑤)
            }
         }
      else {                               //저가가 내일SAR을 하향돌파했다면
           추세 = -1;                      //추세는 하락으로 전환되고
           당일 = 극대[1];                 //당일SAR은 EP(+)값으로 대체(⑧)
           극대 = 0;                       //하락추세로 전환되었으므로 EP(+)는 0이 되고(④)
           극저 = l;                       //EP(-)는 현재 저가로(⑩)
           가속 = af;                      //AF는 최초 값으로(⑥)
           }
      }     
   }      
내일 = (max(극대,극저) - 당일) * 가속 + 당일;    //당일SAR과 EP(+),EP(-),AF가 모두 구해졌으므로 내일SAR 계산 가능
 
plot1(당일);
[출처] [지표식]파라볼릭 SAR 계산식 |작성자 쇠푼이
