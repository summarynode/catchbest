파라볼릭을 제외한 함수는 저희 예스랭귀지 사용자 함수에서 
보실 수 있습니다.
[파라볼릭]

Input : Af(Numeric), MaxAf(Numeric);
Var : direction(0), sar1(0), afval(0), ep(0),lowval(0),highval(0),count(0);

if count == 0 then //변수들의 초기화 
{
    highval = High;
    lowval = Low;
    direction = 0;
    sar1 = 0;
    afval = 0;
    ep = 0;
    count = 1;
}

if(ep != 0) then   // 추세가 진행중 
{
    if(direction == 1) then   // 상승추세이면 
    {
        ep = highval;                               
        sar1 = sar1 + afval*(ep-sar1);      // 상승추세에서 SAR값 계산
        if(high > highval) then                  // 신고가 발생
        {
            highval = high;                        
            afval = afval+Af;                  // 상승추세에서 신고가가 발생했으므로 가속변수 증가
            if(afval >= MaxAf) then            // 가속변수가 최대값과의 비교 (가속변수는 최대 가속값 보다 클 수 없다)
                  afval = MaxAf;
        }
        if( low < sar1) then                  // 추세변경(상승->하락)
        {                                                // 다음 SAR값을 계산할 때 필요한 전SAR값은 하락추세로 전환되기 직전의
            direction = -1;                        // 최고가를 사용하므로 sar1 = ep (ep에는 highval 들어있다)
            sar1 = ep;                              // 추세가 변경되었으므로 가속변수 및 EP, highval 초기화
            afval = 0;
            ep = 0;
            lowval = low;
        }
    }
    else                                                // 하락 추세
    {
        ep = lowval;                              // 하락추세에서는 EP로 저가 사용
        sar1 = sar1 + afval*(ep-sar1);
        if(low < lowval) then                  // 신저가 발생
        {
            lowval = low;                        
            afval = afval + Af;
            if( afval >= MaxAf) then       // 가속변수가 최대값과의 비교 (가속변수는 최대 가속값 보다 클 수 없다)
                  afval = MaxAf;
        }
        if(high > sar1) then                  // 추세 변경(하락->상승)
        {                                                // 다음 SAR 값을 계산할 때 필요한 전SAR값은 상승추세로 전환되기 직전의 
            direction = 1;                        // 최저가를 사용하므로 sar1 = ep (ep에는 lowval값이 들어있다)
            sar1 = ep;                              // 추세가 변경되었으므로 가속변수 및 EP, highval 초기화
            afval = 0;
            ep = 0;
            highval = high;
        }
     }
}

else if(sar1 != 0 && ep == 0) then            //추세가 변경된후 첫번째 SAR계산       
{
      if(direction == 1) then                        // 상승 추세
      {
         ep = highval;                              // 상승추세에서는 신고가를 EP로 사용하므로 
         afval = Af;                                    // 가속변수의 초기값인 AF(0.02) 적용
         sar1 = sar1 + afval*(ep-sar1);      
         if ( high > highval) then          // 신고가가 발생
         {
             Highval = high;
             afval = afval + Af;                  // 가속변수 증가
             if(afval >= MaxAf) then            // 가속변수가 최대값과의 비교 (가속변수는 최대 가속값 보다 클 수 없다)
                   afval = MaxAf;
         }
       }
       else                                                // 하락추세
       {
            ep = lowval;                        
            afval = Af;                              // 가속변수의 초기값인 AF(0.02) 적용
            sar1 = sar1 + afval*(ep-sar1);// 하락추세에서 SAR 계산 
            if(low < lowval) then                  // 신저가 발생
            {
                lowval = low;
                afval = afval + Af;            // 가속변수 증가
                if(afval >= MaxAf) then       // 가속변수가 최대값과의 비교 (가속변수는 최대 가속값 보다 클 수 없다)
                     afval = MaxAf;
            }
        } 
} 
else      // SAR 첨 시작                                     
{
       if direction == 0 then                        // 추세가 없으므로
       {
         if(c > c[1]) then                              // 상승추세로 시작
               direction = 1;
         else if( c < c[1]) then                  // 하락추세로 시작
                direction = -1;   
      }
       else if direction == 1 then                // 상승추세(추세변경이 일어날 경우 SAR 계산하기 시작)
       {
         if(c < c[1]) then                              // 추세 변경 (상승->하락) SAR 계산하기 시작
         {
             direction = -1;
             sar1 = highval;                        // 하락추세로 전환시 다음 SAR값을 계산할 때 필요한 전 SAR값은 추세직전의 최고가를 사용하므로
         }
      } 
      else if direction == -1 then            // 하락추세(추세변경이 일어날 경우 SAR 계산하기 시작)
      {
         if( c > c[1]) then                        // 추세 변경 (하락->상승) SAR 계산하기 시작      
         {
                   direction = 1;
                   sar1 = lowval;                        // 상승추세로 전환시 다음 SAR값을 계산할 때 필요한 전 SAR값은 추세직전의 최저가를 사용하므로
         }
      }
       lowval = min(low, lowval);                  // 추세 변경시 전 SAR값으로 최고가나 최저가가 필요하므로 
       highval = max(high,highval);            // low, high를 전의 최고가, 최저가와 비교하여 최고가, 최저가를 저장             
}

//처음 시작할 때 다음 추세 변경이 일어날 때까지 SAR는 invalid 
if(sar1 != 0) then      
      User_Func_Sar = sar1;

감사합니다.